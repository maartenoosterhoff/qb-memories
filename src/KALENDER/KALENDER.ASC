' KALENDER.BAS
' Geprogrammeerd door Maarten Oosterhoff, 1991-1992
'
  DECLARE FUNCTION Januari1 (jr)
  '
  DECLARE SUB Accentueer ()
  DECLARE SUB Bekijken ()
  DECLARE SUB Bereken (j)
  DECLARE SUB Bereken1Dag ()
  DECLARE SUB CheckCommand ()
  DECLARE SUB Controleer ()
  DECLARE SUB ControleerWeekNummers ()
  DECLARE SUB Einde ()
  DECLARE SUB Feestdagen (j, jan1)
  DECLARE SUB Help ()
  DECLARE SUB HerstelScherm ()
  DECLARE SUB Info ()
  DECLARE SUB Installeer ()
  DECLARE SUB Jaartal ()
  DECLARE SUB Kaders (x1, x2, y1, y2, f)
  DECLARE SUB KiesUitMenu ()
  DECLARE SUB MaakHokje ()
  DECLARE SUB Monitor ()
  DECLARE SUB Opstelling ()
  DECLARE SUB Output1 ()
  DECLARE SUB Output2 ()
  DECLARE SUB PrintOfSchrijf ()
  DECLARE SUB Restart ()
  DECLARE SUB SchrijfConfig ()
  DECLARE SUB StartKeuze (m, k)
  DECLARE SUB Toets (x, y)
  DECLARE SUB VulScherm (x1, x2)
  DECLARE SUB Wacht (tijd#)
  DECLARE SUB Zoeken ()
'
1 CLEAR , , 4000: TROFF: KEY OFF: CLS : RESTORE MenuData: LOCATE , , 0
  DIM SHARED Menu%(10), m$(3, 6), ml%(3), mp%(6), mk$(6), ma%(6), keys%(10)
  DIM SHARED mnd$(12), m2%(12), m3%(12), dg%(500), d$(7), wknr$(4)
  DIM SHARED f%(12), f2%(12), f3%(4, 2), f$(12), f2$(12), mk%(10), j$(2)
  DIM SHARED beschr$(4, 6), oud$(6), Helper$(16), bekijks$(1000), keuze%(10)
  DIM SHARED h%(16)
  '
  DIM SHARED kal$(4, 25), t$, t2$
  DIM SHARED k%(2, 2, 4)
  DIM SHARED bs$, c$, i$, jaar$, output$, path$
  DIM SHARED eindig, g.intro, j, jan1, jaren, m, kk, k, k2, kl, kl2
  DIM SHARED mm, restarten, mkal, xyz
  FOR a = 1 TO 7
    READ d$(a)
  NEXT a
  FOR a = 1 TO 3
    READ ml%(a), mp%(a), mk$(a), ma%(a)
    FOR b = 1 TO 6
      READ m$(a, b)
      READ beschr$(a, b)
    NEXT b
  NEXT a
  READ mp%(4), mk$(4), beschr$(4, 1), ma%(4)
  FOR a = 1 TO 12
    READ mnd$(a), m2%(a), m3%(a)
    f2%(a) = 0
  NEXT a
  FOR a = 1 TO 10
    READ Menu%(a), keuze%(a)
  NEXT a
  FOR a = 1 TO 12
    READ f$(a), f2$(a)
  NEXT a
  FOR a = 1 TO 16
    READ Helper$(a), h%(a)
  NEXT a
  FOR a = 1 TO 10
    READ mk%(a)
  NEXT a
  jaar$ = RIGHT$(DATE$, 4): j$(1) = jaar$: eindig = 0: g.intro = 0
  kl = 1: kl2 = 0: c$ = COMMAND$: REM Lees KALENDER.CFG (Config bestand)
  OPEN "KALENDER.CFG" FOR RANDOM AS #1 LEN = 8
  FIELD #1, 8 AS lees$: GET #1, 1
  IF lees$ = STRING$(8, 0) THEN
     l$ = CHR$(0) + CHR$(7) + CHR$(7) + CHR$(0) + CHR$(7)
     LSET lees$ = l$ + CHR$(0) + CHR$(0) + CHR$(7): PUT #1, 1
     l$ = CHR$(0) + CHR$(3) + CHR$(11) + CHR$(0) + CHR$(14)
     LSET lees$ = l$ + CHR$(0) + CHR$(15) + CHR$(4): PUT #1, 2
     LSET lees$ = "LPT1:": PUT #1, 3
  END IF
  FOR keer = 1 TO 2: GET #1, keer
    FOR a = 1 TO 4
      FOR b = 1 TO 2
        k%(keer, b, a) = ASC(MID$(lees$, 2 * a - 2 + b, 1))
      NEXT b
    NEXT a
  NEXT keer
  output$ = ""
  FOR b = 3 TO 6: GET #1, b
      FOR a = 1 TO 8: code = ASC(MID$(lees$, a, 1))
          IF code > 32 THEN output$ = output$ + CHR$(code)
      NEXT a
  NEXT b: CLOSE
'
' Kleuren Scherm of Monoschrome Scherm
'
  kl = 2: DEF SEG = &H40                        ' Set BIOS
  IF (PEEK(&H10) AND &H30) = &H30 THEN kl = 1   ' Kijk
  DEF SEG                                       ' Herstel BIOS
'
  IF c$ <> "" THEN
     CALL CheckCommand
     IF output$ = "CON" OR output$ = "PRN" THEN output$ = "LPT1:"
     IF VAL(jaar$) < 1582 OR VAL(jaar$) > 2199 THEN jaar$ = RIGHT$(DATE$, 4)
  END IF
  FOR k = 1 TO 10
    KEY k, CHR$(k)
  NEXT k
  m = 1: k = 1: mm = 1: kk = 1: restarten = 0: mkal = 0
  ON TIMER(1) GOSUB TijdHandeling
  ON ERROR GOTO ErrorHandeling
'
  TIMER ON: GOSUB TijdHandeling: CALL HerstelScherm
  IF eindig <> 1 THEN
    IF g.intro = 0 THEN
       CALL Kaders(6, 10, 13, 66, 1): COLOR k%(kl, 1, 3), k%(kl, 2, 3)
       LOCATE 7, 15: PRINT "     KALENDER is geproduceerd in 1991-1992      ";
       LOCATE 8, 15: PRINT "------------------------------------------------";
       LOCATE 9, 15: PRINT "Kalender Utilities door M. Oosterhoff, *********";
       CALL Wacht(5): CALL VulScherm(6, 10)
    END IF
  END IF
  CALL Bereken(VAL(jaar$))
'
Lus:
'
  CALL KiesUitMenu
  CALL StartKeuze(mm, kk)
  IF eindig = 1 THEN TIMER OFF: CLS : KILL "KALENDER.DAT": CALL SchrijfConfig: END
  i$ = "": IF restarten = 1 THEN RUN 1
  GOTO Lus
'
TijdHandeling:
'
  xx = CSRLIN: yy = POS(0): tt = SCREEN(xx, yy, 1)
  COLOR k%(kl, 1, 4), k%(kl, 2, 4)
  LOCATE 1, 70: PRINT " "; TIME$; " "; : LOCATE xx, yy
  COLOR tt MOD 16 + 16 * (tt \ 128), (tt \ 16) MOD 8: RETURN
'
ErrorHandeling:
'
  eh = ERR: IF eh = 24 OR eh = 57 OR eh = 68 OR eh = 70 OR eh = 71 OR eh = 72 THEN eh = 25
  SELECT CASE eh
    CASE 25
      CALL Kaders(9, 12, 5, 33 + LEN(output$), 1): BEEP: COLOR 20, 0
      LOCATE 10, 7: PRINT output$; " niet gereed of kapot !!!";
      LOCATE 11, 7: PRINT "Druk op een toets...";
      WHILE INKEY$ = "":  WEND
      CLOSE #1: GOTO ErrorEinde
    CASE 61
      CALL Kaders(9, 14, 5, 47, 1): BEEP: COLOR 20, 0
      LOCATE 10, 7: PRINT "Disk(ette) is vol !!!";
      LOCATE 11, 7: PRINT "Zorg dat er genoeg ruimte vrij komt !!!";
      LOCATE 13, 7: PRINT "Druk op een toets...";
      WHILE INKEY$ = "": WEND
      GOTO ErrorEinde
    CASE 76
      CLOSE #2: MKDIR LEFT$(path$, LEN(path$) - 1):
      OPEN path$ + "KAL_COPY.EXE" FOR RANDOM AS #2 LEN = 255
      RESUME NEXT
      ' Maak Directory aan als hij niet bestaat en ga verder
    CASE ELSE
      CLS : PRINT ERR; ERL; : PRINT "Fout opgetreden, programma gestopt."
    PRINT "Fout:"; ERR; ERL: END
  END SELECT
'
ErrorEinde:
'
  IF eindig = 1 THEN TIMER OFF: CLS : KILL "KALENDER.DAT": CALL SchrijfConfig: END
  IF FRE(-2) <= 50 THEN CLS : LOCATE 1, 10: PRINT "KALENDER V1 moet herstarten!": BEEP: a$ = INPUT$(1): RUN 1
  IF restarten = 1 THEN RUN 1
  i$ = "": RESUME Lus
'
MenuData:
'
  DATA "Maandag","Dinsdag","Woensdag","Donderdag","Vrijdag","Zaterdag","Zondag"
  DATA 10,15,"Beheer",3
  DATA "Info      "
  DATA "Informatie over het programma"
  DATA "Installeer"
  DATA "Installeer Kalender ergens anders"
  DATA "Einde     "
  DATA "Het einde van Kalender"
  DATA ""
  DATA ""
  DATA ""
  DATA ""
  DATA ""
  DATA ""
  DATA 17,26,"Opties",6
  DATA "Herstart         "
  DATA "Kalender wist de oude waarden en begint opnieuw"
  DATA "Bekijken         "
  DATA "Een kalender doorlopen"
  DATA "Output produceren"
  DATA "Het printen/wegschrijven van een kalender"
  DATA "Zoeken           "
  DATA "Zoek de feestdagen en vermeld ze"
  DATA "Controleren      "
  DATA "Controleer de ingevoerde gegevens"
  DATA "Bereken 1 dag    "
  DATA "Bereken de dag waarop de door U gekozen datum valt"
  DATA 10,37,"Instellingen",4
  DATA "Jaartal   "
  DATA "Jaartal invoeren"
  DATA "Output    "
  DATA "Kies het de plaats waar de kalender heengeschreven moet worden"
  DATA "Opstelling"
  DATA "Moet de speciale (feest)dagen apart vermeld worden"
  DATA "Monitor   "
  DATA "Stel het beeldscherm in op een kleuren/monochrome monitor"
  DATA ""
  DATA ""
  DATA ""
  DATA ""
  DATA 55,"Help","Het HELP-Menu - Altijd oproepbaar met F2",1
  DATA "Januari  ", 16, 31
  DATA "Februari ", 31, 28
  DATA "Maart    ", 47, 31
  DATA "April    ", 17, 30
  DATA "Mei      ", 34, 31
  DATA "Juni     ", 49, 30
  DATA "Juli     ", 17, 31
  DATA "Augustus ", 30, 31
  DATA "September", 45, 30
  DATA "Oktober  ", 16, 31
  DATA "November ", 30, 30
  DATA "December ", 45, 31
  DATA 1, 1     
  DATA 4, 1
  DATA 1, 3
  DATA 2, 1
  DATA 2, 2
  DATA 2, 3
  DATA 2, 4
  DATA 3, 1
  DATA 3, 2
  DATA 3, 3
  DATA "Pasen          " ,""
  DATA "Hemelvaart     ", ""
  DATA "Pinksteren     ", ""
  DATA "Goede Vrijdag  ", ""
  DATA "Koninginnedag  " ,"30 Apr"
  DATA "Dodenherdenking", "4 Mei"
  DATA "Bevrijdingsdag ", "5 Mei"
  DATA "Dierendag      ", "4 Okt"
  DATA "St Maarten     ", "11 Nov"
  DATA "Sinterklaas    ", "5 Dec"
  DATA "Kerst          ", "25/26 Dec"
  DATA "Oud en nieuw   ", "31 Dec/1 Jan"
  DATA "Help op Help             ",   1
  DATA "Help op Kalender         ",  10
  DATA "Help op Info             ",  34
  DATA "Help op Installeer       ",  41
  DATA "Help op Einde            ",  47
  DATA "Help op Herstart         ",  53
  DATA "Help op Bekijken         ",  59
  DATA "Help op Output produceren",  67
  DATA "Help op Zoeken           ",  74
  DATA "Help op Controleren      ",  84
  DATA "Help op Bereken 1 Dag    ",  89
  DATA "Help op Jaartal          ",  95
  DATA "Help op Output aangeven  ", 105
  DATA "Help op Opstelling       ", 111
  DATA "Help op Monitor          ", 124
  DATA "Terug naar het Menu      ", 133
  DATA 3,10,17,24,32,40,48,55,62,70

SUB Accentueer
'
  CALL VulScherm(20, 22): l = 40 - LEN(beschr$(m, k)) / 2
  CALL Kaders(20, 22, l - 2, 80 - l + 1, 1)
  COLOR k%(kl, 1, 3), k%(kl, 2, 3)
  LOCATE 21, l: PRINT beschr$(m, k);
  IF m = 4 THEN EXIT SUB: ' Help heeft geen keuzes
  FOR x = 1 TO ma%(m)
    LOCATE x + 2, mp%(m): PRINT m$(m, x);
  NEXT x
  COLOR k%(kl, 1, 1), k%(kl, 2, 1)
  LOCATE k + 2, mp%(m): PRINT m$(m, k);
'
END SUB

SUB Bekijken
'
  IF mkal = 1 THEN
     CALL Kaders(7, 9, 25, 66, 1): COLOR k%(kl, 1, 3), k%(kl, 2, 3)
     LOCATE 8, 27: PRINT "Niet mogelijk met meerdere kalenders!":
     CALL Wacht(2.5)
  END IF: IF mkal = 1 THEN EXIT SUB
  OPEN "KALENDER.DAT" FOR INPUT AS #1: e = 0
  WHILE NOT (EOF(1))
    e = e + 1: INPUT #1, bekijks$(e)
  WEND
  CLOSE #1: be = e: e = 1
'
BeginBekijk:
'
  CALL Kaders(3, 21, 13, 67, 1): COLOR k%(kl, 1, 3), k%(kl, 2, 3)
  LOCATE 3, 17: PRINT " Gebruik: "; CHR$(24); ", "; CHR$(25);
  PRINT ", PgUp, PgDn, Home, End en <Esc> "; : p = 4
'
Bekijk:
'
  p = 4
  FOR a = e TO e + 16
    LOCATE p, 15: PRINT STRING$(51, 32);
    LOCATE p, 15: PRINT bekijks$(a); : p = p + 1
  NEXT a: i$ = "": WHILE i$ = "": i$ = INKEY$: WEND
  IF i$ = CHR$(0) + CHR$(80) THEN e = e + 1
  IF i$ = CHR$(0) + CHR$(72) THEN e = e - 1
  IF i$ = CHR$(0) + CHR$(73) THEN e = e - 10
  IF i$ = CHR$(0) + CHR$(81) THEN e = e + 10
  IF i$ = CHR$(0) + CHR$(71) THEN e = 1
  IF i$ = CHR$(0) + CHR$(79) THEN e = be
  IF i$ = CHR$(13) OR i$ = CHR$(27) THEN EXIT SUB
  IF i$ = CHR$(2) THEN CALL Help: CALL HerstelScherm: GOTO BeginBekijk
  IF e < 1 THEN e = 1
  IF e + 18 > be THEN e = be - 18
  GOTO Bekijk
'
END SUB

SUB Bereken (j)
'
  CALL Kaders(8, 13 - mkal, 11, 69, 1): COLOR k%(kl, 1, 3), k%(kl, 2, 3)
  jaar = j: jan1 = Januari1(j)
  LOCATE 9, 13: PRINT "1 Januari van het jaar"; STR$(jaar); " valt op een "; d$(jan1); TAB(66); "Ok";
  dag = jan1
  FOR maand = 1 TO 12
    LOCATE 10, 13: COLOR k%(kl, 1, 3), k%(kl, 2, 3)
    PRINT "Bezig met de maand: "; mnd$(maand); "   ";
    FOR dag2 = 1 TO m3%(maand)
      y = LEN(STR$(dag))
      dg%(dag + dag2 - 1) = dag2
    NEXT dag2
    dag = dag + m3%(maand) + 7
  NEXT maand: COLOR k%(kl, 1, 3), k%(kl, 2, 3)
  LOCATE 10, 13: PRINT "Klaar met de maanden"; TAB(66); "Ok";
  LOCATE 11, 13: PRINT "Bezig met de feest- of speciale dagen";
  CALL Feestdagen(jaar, jan1): LOCATE 11, 13:
  COLOR k%(kl, 1, 3), k%(kl, 2, 3): PRINT "Klaar";
  LOCATE 11, 66: PRINT "Ok"; : IF mkal = 1 THEN EXIT SUB
  LOCATE 12, 13: PRINT "Kalender schrijven";
  OPEN "KALENDER.DAT" FOR OUTPUT AS #1
  FOR a = 1 TO 12 STEP 3
    COLOR k%(kl, 1, 3), k%(kl, 2, 3): PRINT ".";
    PRINT #1, : PRINT #1, : PRINT #1, STRING$(23, 255); jaar$
    PRINT #1, : PRINT #1, STRING$(m2%(a) - 5, 255); mnd$(a);
    spaties = LEN(mnd$(a)) + m2%(a)
    PRINT #1, STRING$(m2%(a + 1) - spaties, 255); mnd$(a + 1);
    spaties = m2%(a + 1) + LEN(mnd$(a + 1))
    PRINT #1, STRING$(m2%(a + 2) - spaties, 255); mnd$(a + 2)
    x = ((a + 2) / 3)
    CALL ControleerWeekNummers
    IF NOT (dg%(106 * x + (6 * (x - 1)))) <> 0 THEN wknr$(x) = LEFT$(wknr$(x), 45)
    PRINT #1, : PRINT #1, STRING$(3, 255); wknr$(x): PRINT #1,
    FOR b = 1 TO 7
      COLOR k%(kl, 1, 3), k%(kl, 2, 3): PRINT ".";
      PRINT #1, LEFT$(d$(b), 2); "  ";
      FOR c = (x - 1) * 16 TO ((x - 1) * 16) + 15
        y = dg%(b + (c * 7))
        IF y = 0 THEN PRINT #1, "   ";  ELSE PRINT #1, USING "## "; y;
      NEXT c: PRINT #1,
    NEXT b
  NEXT a: PRINT #1, : PRINT #1,
  PRINT TAB(66); "Ok"; : CLOSE #1
'
END SUB

SUB Bereken1Dag
'
  DIM inv$(3)
  FOR a = 1 TO 3
    inv$(a) = ""
  NEXT a
  FOR d = 1 TO 3
'
Lusje:
'
    CALL Kaders(7, 13, 23, 59, 1): COLOR k%(kl, 1, 3), k%(kl, 2, 3)
    LOCATE 8, 25:  PRINT "Jaar : "; inv$(1);
    LOCATE 9, 25:  PRINT "Maand: "; inv$(2);
    LOCATE 10, 25: PRINT "Dag  : "; inv$(3);
    ok = 0: WHILE ok = 0: LOCATE d + 7, 32: i$ = ""
    PRINT inv$(d); " "; : LOCATE , POS(0) - 1, 1
    WHILE i$ = "": i$ = INKEY$: WEND
    IF i$ = CHR$(27) THEN GOTO Bereken1DagEinde
    IF i$ = CHR$(8) AND LEN(inv$(d)) = 0 THEN i$ = ""
    IF i$ = CHR$(8) THEN inv$(d) = LEFT$(inv$(d), LEN(inv$(d)) - 1): i$ = ""
    IF i$ = CHR$(13) AND inv$(d) = "" THEN i$ = ""
    IF i$ = CHR$(13) AND d = 1 AND (VAL(inv$(d)) < 1582 OR VAL(inv$(d)) > 2199) THEN GOSUB Ongeldig: i$ = ""
    IF i$ = CHR$(13) AND d = 2 AND (VAL(inv$(d)) < 1 OR VAL(inv$(d)) > 12) THEN GOSUB Ongeldig: i$ = ""
    IF i$ = CHR$(13) AND d = 3 THEN
       IF VAL(inv$(d)) < 1 OR VAL(inv$(d)) > m3%(VAL(inv$(2))) THEN GOSUB Ongeldig: i$ = ""
    END IF
    IF i$ = CHR$(13) THEN i$ = "": ok = 1
    IF i$ = CHR$(2) THEN LOCATE , , 0: CALL Help: CALL HerstelScherm: GOTO Lusje
    IF INSTR("0123456789", i$) <> 0 AND LEN(inv$(d)) = 4 THEN i$ = ""
    IF INSTR("0123456789", i$) <> 0 THEN inv$(d) = inv$(d) + i$
    i$ = "": WEND: IF d = 1 THEN jan1 = Januari1(VAL(inv$(d)))
  NEXT d: REM Gegevens ingevoerd!
  '
  FOR a = 2 TO 3
    IF LEN(inv$(a)) = 1 THEN inv$(a) = "0" + inv$(a)
  NEXT a: dag = jan1
  FOR a = 1 TO VAL(inv$(2)) - 1
    dag = dag + m3%(a)
  NEXT a
  dag = dag + VAL(inv$(3)) - 1: dag = dag MOD 7
  LOCATE 12, 25, 0: IF dag = 0 THEN dag = 7
  PRINT inv$(3); "-"; inv$(2); "-"; inv$(1); " valt op een "; d$(dag); ".";
  CALL Toets(8, 65): WHILE INKEY$ = "": WEND: GOTO Bereken1DagEinde
'
Ongeldig:
'
  LOCATE 12, 25, 0: PRINT "Ongeldige datum!"; : BEEP
  WHILE INKEY$ = "": WEND
  LOCATE 12, 25: PRINT STRING$(16, 32); : RETURN
'
Bereken1DagEinde:
'
  LOCATE , , 0
'
END SUB

SUB CheckCommand
'
  c3$ = ""
  FOR a = 1 TO LEN(c$)
    IF ASC(MID$(c$, a, 1)) > 32 THEN c3$ = c3$ + MID$(c$, a, 1)
  NEXT: c$ = c3$: c3$ = "": p = 0
  FOR a = 1 TO LEN(c$)
    IF MID$(c$, a, 1) = "/" THEN p = p + 1
    code = ASC(MID$(c$, a, 1))
    IF code >= 97 AND code <= 122 THEN code = code - 32
    MID$(c$, a, 1) = CHR$(code)
  NEXT a: DIM c2$(p)
  FOR a = 1 TO p
    x = INSTR(2, c$, "/")
    IF x = 0 THEN c2$(a) = c$ ELSE c2$(a) = MID$(c$, 1, x - 1)
    c$ = RIGHT$(c$, LEN(c$) - x + 1)
  NEXT a
  FOR a = 1 TO p
    y = LEN(c2$(a)): c2$(a) = RIGHT$(c2$(a), y - 1): c$ = c2$(a)
    IF c$ = "B" THEN i$ = CHR$(5): GOTO VC: REM VC = VolgendeCommando
    IF c$ = "P" THEN i$ = CHR$(6): GOTO VC
    IF LEFT$(c$, 2) = "J=" THEN jaar$ = RIGHT$(c$, y - 3): GOTO VC
    IF LEFT$(c$, 2) = "O=" THEN output$ = RIGHT$(c$, y - 3): GOTO VC
    IF c$ = "X" AND (i$ = CHR$(5) OR i$ = CHR$(6)) THEN eindig = 1: GOTO VC
    IF c$ = "K" THEN kl = 2: kl2 = 1: GOTO VC
    IF c$ = "M" THEN kl = 1: kl2 = 1: GOTO VC
    IF c$ = "G" THEN g.intro = 1: GOTO VC
    IF c$ = "H" OR c$ = "?" THEN ok = 0 ELSE ok = 1
    LOCATE 1, 1: PRINT "  KALENDER V1 - Kalender Utilities"
    PRINT "  --------------------------------": PRINT
    PRINT "  Start Opties: KALENDER <opties>": PRINT
    IF ok = 1 THEN PRINT "  Foute optie: /"; c$; : PRINT
    PRINT : PRINT "  /B           => Bekijk gelijk een kalender"
    PRINT "  /P           => Print meteen een kalender"
    PRINT "  /J=<jaar>    => Jaar instellen"
    PRINT "  /O=<uitgang> => Printuitgang instellen"
    PRINT "  /K           => Kleurenscherm instellen"
    PRINT "  /M           => Monochrome beeldscherm"
    PRINT "  /G           => Geen intro"
    PRINT "  /X           => Eindig meteen na /B of /P"
    PRINT : END
'
VC:
'
  NEXT a: ERASE c2$: c$ = "": c3$ = "": x = 0: ok = 0: y = 0
'
END SUB

SUB Controleer
'
BeginControleer:
'
  COLOR k%(kl, 1, 2), k%(kl, 2, 2): p = 0
  FOR a = 1 TO 12
    IF f2%(a) = 1 THEN p = p + 1
  NEXT a: CALL Kaders(3, 9 + p, 10, 55, 1)
  COLOR k%(kl, 1, 3), k%(kl, 2, 3)
  LOCATE 4, 15: PRINT "Gegevens van de kalender";
  LOCATE 6, 12: IF mkal = 0 THEN PRINT "Jaar      : ";  ELSE PRINT "Jaren     : ";
  PRINT jaar$;
  LOCATE 7, 12: PRINT "Outputfile: "; output$;
  LOCATE 8, 12: IF p = 0 THEN x$ = "geen": x2$ = "." ELSE x$ = "de volgende": x2$ = ":"
  PRINT "U wilt "; x$; " feestdagen printen"; x2$; : p = 9
  FOR a = 1 TO 12
    IF f2%(a) = 1 THEN LOCATE p, 14: PRINT "- "; f$(a); : p = p + 1
  NEXT a: CALL Toets(5, 60): h$ = "": WHILE h$ = "": h$ = INKEY$
  IF h$ = CHR$(2) THEN CALL Help: CALL HerstelScherm: GOTO BeginControleer
  WEND
'
END SUB

SUB ControleerWeekNummers
'
  wknr$(1) = "  1  2  3  4  5  5  6  7  8  9  9 10 11 12 13 14"
  wknr$(2) = " 14 15 16 17 18 18 19 20 21 22 22 23 24 25 26 27"
  wknr$(3) = " 27 28 29 30 31 31 32 33 34 35 35 36 37 38 39 40"
  wknr$(4) = " 40 41 42 43 44 44 45 46 47 48 48 49 50 51 52 53"
  IF dg%(6) <= 1 THEN MID$(wknr$(1), 18, 1) = "6"
  IF m3%(2) = 29 AND dg%(43) <= 1 THEN MID$(wknr$(1), 32, 2) = "10"
  IF m3%(2) = 28 AND dg%(44) <= 1 THEN MID$(wknr$(1), 32, 2) = "10"
  IF dg%(119) <= 1 THEN MID$(wknr$(2), 17, 2) = "19"
  IF dg%(152) <= 1 THEN MID$(wknr$(2), 32, 2) = "23"
  IF dg%(229) <= 1 THEN MID$(wknr$(3), 17, 2) = "32"
  IF dg%(264) <= 1 THEN MID$(wknr$(3), 32, 2) = "36"
  IF dg%(342) <= 1 THEN MID$(wknr$(4), 17, 2) = "45"
  IF dg%(378) <= 1 THEN MID$(wknr$(4), 32, 2) = "49"
  '
END SUB

SUB Einde
'
BeginEinde:
'
  CALL Kaders(7, 9, 25, 55, 1): COLOR k%(kl, 1, 3), k%(kl, 2, 3)
  LOCATE 8, 27, 1: PRINT "Kalender verlaten (J/N) ? "; : i$ = ""
  WHILE UCASE$(i$) <> "J" AND UCASE$(i$) <> "N" AND i$ <> CHR$(27) AND i$ <> CHR$(2)
  i$ = INKEY$: WEND: LOCATE , , 0
  IF i$ = "J" OR i$ = "j" THEN eindig = 1
  IF i$ = CHR$(2) THEN CALL Help: CALL HerstelScherm: GOTO BeginEinde
'
END SUB

SUB Feestdagen (j, jan1)
'
  IF j < 1700 THEN m2 = 22: n = 2
  IF j < 1800 THEN m2 = 23: n = 3
  IF j < 1900 THEN m2 = 23: n = 4
  IF j < 2100 THEN m2 = 24: n = 5
  IF j < 2200 THEN m2 = 24: n = 6
  a = j - 19 * INT(j / 19): b = j - 4 * INT(j / 4): c = j - 7 * INT(j / 7)
  d = 19 * a + m2 - 30 * INT((19 * a + m2) / 30): e = 2 * b + 4 * c + 6 * d + n - 7 * INT((2 * b + 4 * c + 6 * d + n) / 7)
  p = 22 + d + e: IF p < 32 THEN mp = 0 ELSE p = d + e - 9: mp = 1: IF p = 26 THEN p = 19 ELSE IF p = 25 AND d = 28 AND a > 10 THEN p = 18
  IF mp = 0 THEN IF p = 22 THEN mh = 1: h = 30: GOTO f1 ELSE mh = 2: h = p - 22: GOTO f1
  IF p < 23 THEN mh = 2: h = p + 9 ELSE mh = 3: h = p - 22
'
f1:
'
  IF mh = 1 THEN mx = 2: x = 10: GOTO f2
  IF mh = 2 THEN IF h < 22 THEN mx = 2: x = h + 10: GOTO f2 ELSE mx = 3: x = h - 21: GOTO f2
  mx = 3: x = h + 10
'
f2:
'
  p = p - 1: h = h - 1: x = x - 1: mp = mp + 3: mh = mh + 3: mx = mx + 3: pro = 0
  f3%(1, 1) = p: f3%(1, 2) = mp: f3%(2, 1) = h: f3%(2, 2) = mh
  f3%(3, 1) = x: f3%(3, 2) = mx: REM Leg waarden voor Pasen/Hemelvaart/Pinksteren vast.
  ' Pasen
  f%(1) = jan1 + p + 6: FOR a = 1 TO mp - 1
  f%(1) = f%(1) + m3%(a) + 7: NEXT a
  ' Hemelvaart
  f%(2) = jan1 + h + 6: FOR a = 1 TO mh - 1
  f%(2) = f%(2) + m3%(a) + 7: NEXT a
  ' Pinksteren
  f%(3) = jan1 + x + 6: FOR a = 1 TO mx - 1
  f%(3) = f%(3) + m3%(a) + 7: NEXT a
  ' Goede Vrijdag
  x = f%(1) MOD 7: IF x = 0 THEN x = 7
  IF x > 5 THEN f%(4) = f%(1) - (x - 5) ELSE f%(4) = f%(1) - (x - 5) - 7
  f3%(4, 1) = f3%(1, 1) - 2: f3%(4, 2) = f3%(1, 2)
  IF dg%(f%(4)) = 0 THEN
    f3%(4, 2) = f3%(4, 2) - 1
    f3%(4, 1) = dg%(f%(4) - 7)
    f%(4) = f%(4) - 7
  END IF
  f%(4) = f%(4) + 6
  ' Koninginnedag
  f%(5) = jan1 + 36: FOR a = 1 TO 3
  f%(5) = f%(5) + m3%(a) + 7: NEXT a
  ' Dodenherdenking
  f%(6) = jan1 + 10: FOR a = 1 TO 4
  f%(6) = f%(6) + m3%(a) + 7: NEXT a
  ' Bevrijdingsdag
  f%(7) = f%(6) + 1
  ' Dierendag
  f%(8) = jan1 + 10: FOR a = 1 TO 9
  f%(8) = f%(8) + m3%(a) + 7: NEXT a
  ' St Maarten
  f%(9) = jan1 + 17: FOR a = 1 TO 10
  f%(9) = f%(9) + m3%(a) + 7: NEXT a
  ' Sinterklaas
  f%(10) = jan1 + 11: FOR a = 1 TO 11
  f%(10) = f%(10) + m3%(a) + 7: NEXT a
  ' Kerst
  f%(11) = f%(10) + 20
  ' Oud en nieuw
  f%(12) = f%(10) + 26
  FOR a = 1 TO 12: x = f%(a) MOD 7: IF x = 0 THEN x = 7
  f%(a) = x: NEXT a: f%(4) = 5
'
END SUB

SUB Help
'
  CALL VulScherm(2, 22): TIMER OFF: COLOR k%(kl, 1, 2), k%(kl, 2, 2)
  OPEN "KALENDER.HLP" FOR RANDOM AS #1 LEN = 36
  FIELD #1, 36 AS kh$: GET #1, 1: ee = 0
  IF INSTR(kh$, "HELP") = 0 THEN
     CALL Kaders(7, 9, 20, 50, 1): COLOR k%(kl, 1, 3), k%(kl, 2, 3)
     BEEP: LOCATE 8, 22: PRINT "Help-bestand niet aanwezig!";
     CALL Toets(11, 25): WHILE INKEY$ = "": WEND
     CALL VulScherm(7, 20): ee = 1
  END IF: IF ee = 1 THEN ee = 0: GOTO HelpEinde
  x1 = 12: y1 = 39: x2 = 14: y2 = 41: xp = 1: yp = 3.5
'
  klaar = 0: b = 0: t = TIMER: WHILE klaar = 0: b = b + 1
  CALL Kaders(x1, x2, y1, y2, 1)
  IF b = 3 THEN
     t = TIMER - t: t = t * 10
     SELECT CASE t
       CASE IS > .25
       xp = xp * 2: yp = yp * 2
       CASE IS > 1
       xp = xp * 1.5: yp = yp * 1.5
       CASE IS > 1.5
       xp = xp * 1.5: yp = yp * 1.5
       CASE ELSE
     END SELECT
  END IF
  IF x1 = 1 AND y1 = 1 AND x2 = 25 AND y2 = 80 THEN klaar = 1
  x1 = x1 - xp: y1 = y1 - yp: x2 = x2 + xp: y2 = y2 + yp
  IF INT(x1) < 1 THEN x1 = 1
  IF INT(y1) < 1 THEN y1 = 1
  IF INT(x2) > 25 THEN x2 = 25
  IF INT(y2) > 80 THEN y2 = 80
  WEND: k2 = 1
'
GeefHelp:
'
  CALL Kaders(8, 17, 13, 67, 1)
  LOCATE 3, 1: PRINT CHR$(199); STRING$(78, 196); CHR$(182);
  LOCATE 23, 1: PRINT CHR$(199); STRING$(78, 196); CHR$(182);
  COLOR k%(kl, 1, 3), k%(kl, 2, 3): LOCATE 2, 31: PRINT "KALENDER V1 HELP";
  LOCATE 24, 3: PRINT "Gebruik: "; CHR$(24); ", "; CHR$(25); ", PgUp, PgDn, <En";
  PRINT "ter> = Bevestigen, <Esc> = Terug naar het menu";
  LOCATE 5, 35: PRINT "HELP MENU";
  FOR a = 1 TO 8: LOCATE a + 8, 15: PRINT Helper$(a);
  LOCATE a + 8, 42: PRINT Helper$(a + 8); : NEXT a
'
Helpmenu:
'
  k2oud = k2: IF k2 > 8 THEN LOCATE k2, 42 ELSE LOCATE k2 + 8, 15
  COLOR k%(kl, 1, 1), k%(kl, 2, 1): PRINT Helper$(k2);
  COLOR k%(kl, 1, 3), k%(kl, 2, 3): i$ = ""
  WHILE i$ = "": i$ = INKEY$: WEND
  IF i$ = CHR$(0) + CHR$(80) THEN k2 = k2 + 1: IF k2 = 17 THEN k2 = 1
  IF i$ = CHR$(0) + CHR$(72) THEN k2 = k2 - 1: IF k2 = 0 THEN k2 = 16
  IF i$ = CHR$(0) + CHR$(77) THEN k2 = k2 + 8: IF k2 > 16 THEN k2 = k2 - 16
  IF i$ = CHR$(0) + CHR$(75) THEN k2 = k2 - 8: IF k2 < 1 THEN k2 = k2 + 16
  IF i$ = CHR$(13) THEN
     IF k2 < 16 THEN GOSUB StartHelp: GOTO GeefHelp ELSE i$ = CHR$(27)
  END IF
  IF i$ = CHR$(27) THEN CALL HerstelScherm: GOTO HelpEinde
  IF i$ = CHR$(0) + CHR$(73) THEN k2 = 1
  IF i$ = CHR$(0) + CHR$(81) THEN k2 = 16
  IF k2 <> k2oud THEN i$ = ""
  IF k2oud > 8 THEN LOCATE k2oud, 42 ELSE LOCATE k2oud + 8, 15
  PRINT Helper$(k2oud); : GOTO Helpmenu
'
StartHelp:
'
  GOSUB HelpMaakSchoon
  IF k2 = 2 THEN
     LOCATE 5, 30: GET #1, 10: PRINT kh$;
     CALL Kaders(7, 19, 19, 58, 1): COLOR k%(kl, 1, 3), k%(kl, 2, 3)
     l = 8: FOR a = 11 TO 21
     GET #1, a: LOCATE l, 21: PRINT kh$; : l = l + 1
     NEXT a: CALL Toets(10, 64): WHILE INKEY$ = "": WEND
     GOSUB HelpMaakSchoon: CALL Kaders(7, 19, 19, 58, 1)
     COLOR k%(kl, 1, 3), k%(kl, 2, 3)
     LOCATE 5, 30: GET #1, 22: PRINT kh$;
     l = 8: FOR a = 23 TO 33: LOCATE l, 21
     GET #1, a: PRINT kh$; : l = l + 1: NEXT a
     CALL Toets(10, 64): WHILE INKEY$ = "": WEND
  ELSE
     LOCATE 5, 30: GET #1, h%(k2): PRINT kh$;
     l = 8: FOR a = h%(k2) + 1 TO h%(k2 + 1) - 1
     GET #1, a: l = l + 1: NEXT a
     CALL Kaders(7, l, 19, 58, 1): COLOR k%(kl, 1, 3), k%(kl, 2, 3)
     l = 8: FOR a = h%(k2) + 1 TO h%(k2 + 1) - 1: LOCATE l, 21
     GET #1, a: PRINT kh$; : l = l + 1: NEXT a
     CALL Toets(10, 64): WHILE INKEY$ = "": WEND
  END IF
'
HelpMaakSchoon:
'
  FOR a = 4 TO 22: LOCATE a, 2: PRINT STRING$(78, 32); : NEXT a: RETURN
'
HelpEinde:
'
  TIMER ON: CLOSE #1
'
END SUB

SUB HerstelScherm
'
  COLOR k%(kl, 1, 4), k%(kl, 2, 4)
  LOCATE 1, 1: PRINT STRING$(80, 32);
  LOCATE 1, 2: PRINT "KALENDER V1"; TAB(15); "Beheer     Opties     Instellingen      Help";
  CALL Kaders(23, 25, 1, 80, 2): CALL VulScherm(2, 22)
  COLOR k%(kl, 1, 4), k%(kl, 2, 4)
  LOCATE 24, 3: PRINT "1 Info 2 Help 3 Eind 4 Start 5 Bek˜k 6 Outpt 7 Zoek 8 Jaar 9 Outpt 10 Opstl ";
  COLOR k%(kl, 1, 3), k%(kl, 2, 3)
  FOR a = 1 TO 10
    LOCATE 24, mk%(a)
    IF a < 10 THEN PRINT CHR$(a + 48);  ELSE PRINT "10";
  NEXT a
'
END SUB

SUB Info
'
BeginInfo:
'
  CALL Kaders(3, 21, 16, 64, 1): COLOR k%(kl, 1, 3), k%(kl, 2, 3)
  LOCATE 4, 34:  PRINT "KALENDER V1";
  LOCATE 5, 34:  PRINT "----------";
  LOCATE 7, 18:  PRINT "Kalender Utilities V1 is geprogrammeerd door:";
  LOCATE 9, 30:  PRINT "Maarten Oosterhoff";
  LOCATE 10, 30: PRINT "******************";
  LOCATE 11, 30: PRINT "*******, *********";
  LOCATE 12, 30: PRINT "Tel: (0252*) *****";
  LOCATE 14, 23: PRINT "Kalender is gemaakt naar aanleiding";
  LOCATE 15, 21: PRINT "van de programmeerwedstrijd van DOS-GG.";
  LOCATE 17, 19: PRINT "Kalender is gemaakt om kalenders te printen,";
  LOCATE 18, 23: PRINT "met ook nog een paar extra functies.";
  LOCATE 20, 21: PRINT "Voor meer informatie zie het HELP-Menu."; : h$ = ""
  CALL Toets(16, 66): WHILE h$ = "": h$ = INKEY$
  IF h$ = CHR$(2) THEN CALL Help: CALL HerstelScherm: GOTO BeginInfo
  WEND
'
END SUB

SUB Installeer
'
  LOCATE 1, 15, 0, 1, 0: SHELL "DIR K*.* > DIR_KAL1.$$$"
  LOCATE , , 0, 10, 12
  OPEN "DIR_KAL1.$$$" FOR INPUT AS #1: DIM b(5)
  FOR a = 1 TO 5
    b(a) = 0
  NEXT a: WHILE NOT (EOF(1)): INPUT #1, a$
  IF LEFT$(a$, 12) = "KALENDER EXE" THEN b(1) = 1
  IF LEFT$(a$, 12) = "KALENDER HLP" THEN b(2) = 1
  IF LEFT$(a$, 12) = "KALENDER DOC" THEN b(3) = 1
  IF LEFT$(a$, 12) = "KCONFIG  EXE" THEN b(4) = 1
  IF LEFT$(a$, 12) = "KINSTALL BAT" THEN b(5) = 1 ELSE a$ = ""
  WEND: grootte = VAL(MID$(bingo$, 13, 10)): CLOSE #1: KILL "DIR_KAL1.$$$"
  status = 0: FOR a = 1 TO 5: status = status + b(a): NEXT a
  IF status <> 5 THEN
    CLOSE : CALL Kaders(9, 12, 5, 34, 1)
    BEEP: COLOR 20, 0: LOCATE 10, 7: PRINT "Installatie niet mogelijk!";
    LOCATE 11, 7: PRINT "Druk op een toets...";
  ELSE
    CALL Kaders(9, 11, 5, 76, 1): COLOR k%(kl, 1, 3), k%(kl, 2, 3)
    LOCATE 10, 7: PRINT "Path: ";
    LINE INPUT path$: IF path$ = "" THEN GOTO InstalleerEinde
    path$ = UCASE$(path$): CLS : LOCATE 1, 1
    PRINT "KALENDER Installatie V1"; : PRINT
    SHELL "KINSTALL " + path$: PRINT : PRINT "Druk op een toets...";
  END IF: WHILE INKEY$ = "": WEND
'
InstalleerEinde:
'
  status = 0: path$ = "": ERASE b
'
END SUB

SUB Jaartal
'
BeginJaartal:
'
  CALL Kaders(7, 9, 20, 50, 1): COLOR k%(kl, 1, 3), k%(kl, 2, 3)
  LOCATE 8, 22: PRINT "Meerdere kalenders (J/N) ";
  LOCATE , , 1: i$ = "": WHILE i$ <> CHR$(2) AND i$ <> CHR$(27) AND UCASE$(i$) <> "J" AND UCASE$(i$) <> "N"
  i$ = INKEY$: WEND: LOCATE , , 0: IF i$ = CHR$(27) THEN GOTO JaartalEinde
  IF i$ = CHR$(2) THEN CALL Help: CALL HerstelScherm: GOTO BeginJaartal
  IF UCASE$(i$) = "J" THEN mkal = 1 ELSE mkal = 0: FOR a = 1 TO 500: dg%(a) = 0: NEXT a
  jaaroud$ = jaar$: FOR jaarinvoer = 1 TO mkal + 1
'
BeginJaartal2:
'
  CALL Kaders(7, 11 + mkal * 2, 14, 65, 1): COLOR k%(kl, 1, 3), k%(kl, 2, 3)
  IF mkal = 1 THEN
     LOCATE 10, 16: PRINT "Voer 2 jaartallen in. Bijvoorbeeld 1950 en 1960.";
     LOCATE 11, 20: PRINT "Dat zijn kalenders van 1950 t/m 1960.";
  END IF
  LOCATE 10 + 2 * mkal, 25: PRINT "<Enter> = "; j$(1); " (Vorige keuze)";
  jaar$ = ""
  ' Maximaal 4 cijfers
  i$ = "": WHILE i$ <> CHR$(13): LOCATE 8, 34: PRINT STRING$(12, 32);
  LOCATE 8, 35, 1: PRINT "Jaar: "; jaar$;
  WHILE i$ = "": i$ = INKEY$: WEND: LOCATE , , 0
  IF i$ = CHR$(13) THEN GOTO JaartalEnd
  IF i$ = CHR$(27) THEN jaar$ = jaaroud$: GOTO JaartalEinde
  IF i$ = CHR$(8) AND LEN(jaar$) = 0 THEN BEEP: GOTO Volgende
  IF i$ = CHR$(8) THEN jaar$ = LEFT$(jaar$, LEN(jaar$) - 1): GOTO Volgende
  IF i$ = CHR$(2) THEN CALL Help: CALL HerstelScherm: GOTO BeginJaartal2
  IF INSTR("0123456789", i$) = 0 OR LEN(jaar$) = 4 THEN BEEP: GOTO Volgende
  jaar$ = jaar$ + i$
'
Volgende:
'
  i$ = "": WEND
'
JaartalEnd:
'
  j = VAL(jaar$): IF j = 0 THEN jaar$ = jaaroud$: j = VAL(jaar$)
  IF j < 1582 OR j > 2199 THEN BEEP: GOTO Volgende
  jaar2$ = STR$(VAL(jaar$)): j$(jaarinvoer) = RIGHT$(jaar2$, LEN(jaar2$) - 1)
  NEXT jaarinvoer
  IF mkal = 1 THEN IF j$(1) > j$(2) THEN SWAP j$(1), j$(2)
  IF mkal = 1 THEN jaar$ = j$(1) + " t/m " + j$(2)
  IF mkal = 0 THEN CALL VulScherm(7, 7): CALL Bereken(VAL(jaar$)): j$(1) = jaar$
'
JaartalEinde:
'
  i$ = ""
'
END SUB

FUNCTION Januari1 (j)
'
  j1 = INT(j / 100) * 100: j2 = j - j1: x = j2 * 365 + INT(j2 / 4) + 1
  IF INT(j / 400) = j / 400 THEN m3%(2) = 29: i = x - 7 * INT(x / 7): GOTO Gevolg
  IF INT(j2 / 4) = j2 / 4 THEN x = x - 1: m3%(2) = 29 ELSE m3%(2) = 28
  i = x - 7 * INT(x / 7): IF j2 = 0 THEN m3%(2) = 28: i = i + 1
  IF j > 2000 THEN i = i + 1
'
Gevolg:
'
  j3 = INT(j1 / 100): x = j3 - 5: y = x - 7 * INT(x / 7): e = y * -2: jan1 = i + e
'
Gevolg2:
'
  IF jan1 < 1 THEN jan1 = jan1 + 7: GOTO Gevolg2
  IF jan1 > 7 THEN jan1 = jan1 - 7: GOTO Gevolg2
  Januari1 = jan1
'
END FUNCTION

SUB Kaders (x1, x2, y1, y2, f)
'
  COLOR k%(kl, 1, 2), k%(kl, 2, 2): LOCATE x1, y1
  IF f = 1 THEN PRINT CHR$(201); STRING$(y2 - y1 - 1, 205); CHR$(187);
  IF f = 2 THEN PRINT CHR$(218); STRING$(y2 - y1 - 1, 196); CHR$(191);
  FOR a = x1 + 1 TO x2 - 1
  IF f = 1 THEN LOCATE a, y1: PRINT CHR$(186); STRING$(y2 - y1 - 1, 32); CHR$(186);
  IF f = 2 THEN LOCATE a, y1: PRINT CHR$(179); STRING$(y2 - y1 - 1, 32); CHR$(179);
  NEXT a
  LOCATE x2, y1
  IF f = 1 THEN PRINT CHR$(200); STRING$(y2 - y1 - 1, 205); CHR$(188);
  IF f = 2 THEN PRINT CHR$(192); STRING$(y2 - y1 - 1, 196); CHR$(217);
'
END SUB

SUB KiesUitMenu
'
  CALL HerstelScherm
  CALL VulScherm(2, 8)
  CALL MaakHokje
  CALL Accentueer
'
Menu:
'
  WHILE i$ = "": i$ = INKEY$: WEND
  SELECT CASE i$
  CASE CHR$(13)
    CALL VulScherm(2, 22): i$ = ""
    mm = m: kk = k: EXIT SUB
  CASE CHR$(27)
    CALL VulScherm(2, 22): i$ = ""
    mm = 1: kk = 3: EXIT SUB
  CASE CHR$(0) + CHR$(80)
    k = k + 1: i$ = "": IF k = ma%(m) + 1 THEN k = 1
  CASE CHR$(0) + CHR$(72)
    k = k - 1: i$ = "": IF k = 0 THEN k = ma%(m)
  CASE CHR$(0) + CHR$(77)
    m = m + 1: k = 1: i$ = "": IF m = 5 THEN m = 1
    CALL VulScherm(2, 9)
    CALL MaakHokje
  CASE CHR$(0) + CHR$(75)
    m = m - 1: k = 1: i$ = "": IF m = 0 THEN m = 4
    CALL VulScherm(2, 9)
    CALL MaakHokje
  CASE ELSE
    ok = 0: FOR keys = 1 TO 10: IF i$ = CHR$(keys) THEN mm = Menu%(keys): kk = keuze%(keys): CALL VulScherm(2, 8): CALL MaakHokje: ok = 1: EXIT SUB
    NEXT keys: IF ok = 0 THEN BEEP: i$ = ""
  END SELECT
  CALL Accentueer
  GOTO Menu
'
END SUB

SUB MaakHokje
'
  COLOR k%(kl, 1, 4), k%(kl, 2, 4)
  FOR a = 1 TO 4
    LOCATE 1, mp%(a) - 1: PRINT " "; mk$(a); " ";
  NEXT a: COLOR k%(kl, 1, 3), k%(kl, 2, 3)
  LOCATE 1, mp%(m) - 1: PRINT " "; mk$(m); " "; : IF m = 4 THEN EXIT SUB
  LOCATE 2, mp%(m) - 2: PRINT CHR$(218); STRING$(ml%(m) + 2, 196); CHR$(191);
  FOR a = 3 TO 2 + ma%(m)
    LOCATE a, mp%(m) - 2
    PRINT CHR$(179); " "; m$(m, a - 2); " "; CHR$(179);
  NEXT a
  LOCATE 3 + ma%(m), mp%(m) - 2: PRINT CHR$(192); STRING$(ml%(m) + 2, 196); CHR$(217);
'
END SUB

SUB Monitor
'
BeginMonitor:
'
  CALL Kaders(7, 9, 21, 58, 1): COLOR k%(kl, 1, 3), k%(kl, 2, 3)
  IF kl = -1 THEN kl = 1
  LOCATE 8, 23, 1: PRINT "Heeft U een kleurenmonitor (J/N) "; : m$ = ""
  WHILE UCASE$(m$) <> "J" AND UCASE$(m$) <> "N" AND m$ <> CHR$(27) AND m$ <> CHR$(2)
  m$ = INKEY$: WEND: LOCATE , , 0: IF m$ = CHR$(27) THEN EXIT SUB
  IF m$ = CHR$(2) THEN CALL Help: CALL HerstelScherm: GOTO BeginMonitor
  IF UCASE$(m$) = "J" THEN kl = 2 ELSE kl = 1
'
END SUB

SUB Opstelling
'
BeginOpstelling:
'
  CALL Kaders(3, 15, 10, 50, 1): COLOR k%(kl, 1, 3), k%(kl, 2, 3)
  LOCATE 4, 15: PRINT "De opstelling van de feestdagen:";
  LOCATE 6, 12: PRINT CHR$(27); "/"; CHR$(26); " = Niet/Wel printen, ";
  PRINT CHR$(24); " "; CHR$(25); " = Kiezen"
  LOCATE 7, 12: PRINT "<Enter> of <Esc> = Bevestigen";
  LOCATE 9, 12:  PRINT "Pasen          :    Bevrijdingsdag:";
  LOCATE 10, 12: PRINT "Hemelvaart     :    Dierendag     :";
  LOCATE 11, 12: PRINT "Pinksteren     :    St Maarten    :";
  LOCATE 12, 12: PRINT "Goede Vrijdag  :    Sinterklaas   :";
  LOCATE 13, 12: PRINT "Koninginnedag  :    Kerst         :";
  LOCATE 14, 12: PRINT "Dodenherdenking:    Oud en nieuw  :"; : ops = 1
'
OpstellingLus:
'
  FOR a = 1 TO 6
    LOCATE a + 8, 29
    IF f2%(a) = 0 THEN PRINT "N";  ELSE PRINT "J";
    LOCATE a + 8, 48
    IF f2%(a + 6) = 0 THEN PRINT "N";  ELSE PRINT "J";
  NEXT a
  IF ops < 7 THEN LOCATE ops + 8, 29, 1 ELSE LOCATE ops + 2, 48, 1
  i$ = "": WHILE i$ = "": i$ = INKEY$: WEND: LOCATE , , 0
  IF i$ = CHR$(0) + CHR$(77) THEN IF f2%(ops) = 0 THEN f2%(ops) = 1: i$ = CHR$(0) + CHR$(80)
  IF i$ = CHR$(0) + CHR$(75) THEN IF f2%(ops) = 1 THEN f2%(ops) = 0: i$ = CHR$(0) + CHR$(80)
  IF i$ = CHR$(0) + CHR$(80) THEN ops = ops + 1: IF ops = 13 THEN ops = 1
  IF i$ = CHR$(0) + CHR$(72) THEN ops = ops - 1: IF ops = 0 THEN ops = 12
  IF i$ = CHR$(27) OR i$ = CHR$(13) THEN EXIT SUB
  IF i$ = CHR$(2) THEN CALL Help: CALL HerstelScherm: GOTO BeginOpstelling
  GOTO OpstellingLus
'
END SUB

SUB Output1
'                                          
BeginOutput1:
'
  p = 0
  FOR a = 1 TO 12
    IF f2%(a) = 1 THEN p = p + 1
  NEXT a: CALL Kaders(3, 9 + p, 5, 46, 1): COLOR k%(kl, 1, 3), k%(kl, 2, 3)
  LOCATE 4, 10: PRINT "Gegevens van de kalender";
  LOCATE 6, 7: IF mkal = 0 THEN PRINT "Jaar      : ";  ELSE PRINT "Jaren     : ";
  PRINT jaar$: x = INSTR(output$, "\"): WHILE x <> 0: y = x
  x = INSTR(x + 1, output$, "\"): WEND
  LOCATE 7, 7: PRINT "Outputfile: "; RIGHT$(output$, LEN(output$) - y);
  LOCATE 8, 7: IF p = 0 THEN x$ = "geen": x2$ = "." ELSE x$ = "de volgende": x2$ = ":"
  PRINT "U wilt "; x$; " feestdagen printen"; x2$; : p = 9
  FOR a = 1 TO 12
    IF f2%(a) = 1 THEN LOCATE p, 9: PRINT "- "; f$(a); : p = p + 1
  NEXT a: CALL Kaders(4, 8, 48, 79, 1): COLOR k%(kl, 1, 3), k%(kl, 2, 3)
  LOCATE 5, 50: PRINT "Wilt  U de kalender(s) prin-";
  LOCATE 6, 50: PRINT "ten die  aan  deze  gegevens";
  LOCATE 7, 50: PRINT "voldoen (J/N) ? "; : p$ = "": LOCATE , , 1
  WHILE p$ <> "J" AND p$ <> "j" AND p$ <> "N" AND p$ <> "n" AND p$ <> CHR$(27) AND p$ <> CHR$(2)
  p$ = INKEY$: WEND: LOCATE , , 0: IF p$ = CHR$(27) THEN EXIT SUB
  IF p$ = CHR$(2) THEN CALL Help: CALL HerstelScherm: GOTO BeginOutput1
  CALL VulScherm(3, 21)
  IF p$ = "N" OR p$ = "n" THEN EXIT SUB
  '
  IF mkal = 0 THEN OPEN output$ FOR OUTPUT AS #1: CALL PrintOfSchrijf: CLOSE #1: EXIT SUB
  OPEN output$ FOR OUTPUT AS #1: CLOSE #1
  FOR jaren = VAL(j$(1)) TO VAL(j$(2)): j = jaren
    FOR b = 1 TO 500
      dg%(b) = 0
    NEXT b: CALL VulScherm(7, 9)
    CALL Bereken(j): CALL VulScherm(8, 12)
    IF INSTR(output$, ":") < 3 THEN OPEN output$ FOR APPEND AS #1 ELSE OPEN output$ FOR OUTPUT AS #1
    CALL Kaders(7, 9, 30, 50, 1): COLOR k%(kl, 1, 3), k%(kl, 2, 3)
    LOCATE 8, 32: PRINT "Output produceren";
    j = jaren: CALL PrintOfSchrijf
    FOR b = 1 TO 5
      PRINT #1,
    NEXT b
    CLOSE #1: IF INKEY$ <> "" THEN EXIT FOR
  NEXT jaren
'
END SUB

SUB Output2
'
  out$ = ""
'
BeginOutput2:
'
  CALL Kaders(3, 14, 13, 61, 1): COLOR k%(kl, 1, 3), k%(kl, 2, 3)
  LOCATE 4, 15: PRINT "Output aangeven"
  LOCATE 6, 15: PRINT "Voorbeelden:"
  LOCATE 8, 15: PRINT "Voor de printer : LPT1:, LPT2:, LPT3:"
  LOCATE 9, 15: PRINT "Voor een bestand: "; RIGHT$(DATE$, 4); ".KAL";
  LOCATE 11, 15: PRINT "Oude keuze   : "; output$; : LOCATE 12, 15
  PRINT "Dit is een ";
  IF INSTR(output$, ":") < 3 THEN PRINT "bestand." ELSE PRINT "randapparaat."
'
BeginO2:
'
  LOCATE 13, 15: PRINT "Geef uw keuze: "; out$; " ";
  LOCATE 13, 30 + LEN(out$), 1: i$ = "": WHILE i$ = "": i$ = INKEY$: WEND
  LOCATE , , 0: IF i$ = CHR$(13) THEN GOTO Verder
  IF i$ = CHR$(27) THEN out$ = output$: GOTO Verder ELSE i$ = UCASE$(i$)
  IF i$ = CHR$(8) AND LEN(out$) = 0 THEN BEEP: GOTO BeginO2
  IF i$ = CHR$(8) THEN out$ = LEFT$(out$, LEN(out$) - 1): GOTO BeginO2
  IF i$ = CHR$(2) THEN CALL Help: CALL HerstelScherm: GOTO BeginOutput2
  IF LEN(out$) = 30 OR INSTR("ABCDEFGHIJKLMNOPQRSTUVWXYZ1234567890-.=:\", i$) = 0 THEN BEEP: GOTO BeginO2
  out$ = out$ + i$: GOTO BeginO2
'
Verder:
'
  IF out$ = "" THEN out2$ = output$ ELSE out2$ = out$
  x = INSTR(out2$, "\"): WHILE x <> 0: y = x: x = INSTR(x + 1, out2$, "\")
  WEND: x$ = RIGHT$(out2$, LEN(out2$) - y)
  IF x$ = "KALENDER.EXE" OR x$ = "KALENDER.DAT" OR x$ = "SCRN:" OR x$ = "CON" OR x$ = "PRN" OR x$ = "KALENDER.DOC" OR x$ = "KALENDER.HLP" OR x$ = "KALENDER.CFG" OR x$ = "KINSTALL.BAT" OR x$ = "KCONFIG.EXE" THEN BEEP: GOTO BeginO2 ELSE output$ =  _
out2$
'
END SUB

SUB PrintOfSchrijf
'
  CALL ControleerWeekNummers
  IF mkal = 0 THEN PRINT #1, TAB(32); jaar$ ELSE PRINT #1, TAB(31); STR$(j)
  FOR a = 1 TO 12 STEP 3
    PRINT #1, : PRINT #1, : PRINT #1, TAB(m2%(a)); mnd$(a);
    PRINT #1, TAB(m2%(a + 1)); mnd$(a + 1);
    PRINT #1, TAB(m2%(a + 2)); mnd$(a + 2): x = ((a + 2) / 3)
    IF NOT (dg%(106 * x + (6 * (x - 1)))) <> 0 THEN wknr$(x) = LEFT$(wknr$(x), 45)
    PRINT #1, : PRINT #1, "Week nr."; TAB(11); wknr$(x): PRINT #1,
    FOR b = 1 TO 7
      PRINT #1, d$(b); STRING$(9 - LEN(d$(b)) + 2, 32); :
      FOR c = (x - 1) * 16 TO ((x - 1) * 16) + 15
        y = dg%(b + (c * 7))
        IF y = 0 THEN PRINT #1, "   ";  ELSE PRINT #1, USING "## "; y;
      NEXT c
      PRINT #1, TAB(63); STRING$(9 - LEN(d$(b)), 32); d$(b)
    NEXT b
  NEXT a: PRINT #1, : PRINT #1, : p = 1
  FOR a = 1 TO 4
    IF INT(p / 2) = p / 2 THEN t = 38 ELSE t = 5
    IF f2%(a) = 1 THEN PRINT #1, TAB(t); f$(a); " = "; LEFT$(d$(f%(a)), 2); STR$(f3%(a, 1)) + " " + LEFT$(mnd$(f3%(a, 2)), 3);
    p = p + 1
  NEXT a
  FOR a = 5 TO 12
    IF INT(p / 2) = p / 2 THEN t = 38 ELSE t = 5
    IF f2%(a) = 1 THEN PRINT #1, TAB(t); f$(a); " = "; LEFT$(d$(f%(a)), 2); " "; f2$(a); : IF t = 40 THEN PRINT #1,
    p = p + 1
  NEXT a
  FOR a = 1 TO 3
    PRINT #1,
  NEXT a
'
END SUB

SUB Restart
'
BeginRestart:
'
  CALL Kaders(7, 9, 25, 56, 1): COLOR k%(kl, 1, 3), k%(kl, 2, 3)
  LOCATE 8, 27, 1: PRINT "Kalender restarten (J/N) ? "; : i$ = ""
  WHILE UCASE$(i$) <> "J" AND UCASE$(i$) <> "N" AND i$ <> CHR$(27) AND i$ <> CHR$(2)
  i$ = INKEY$: WEND: LOCATE , , 0
  IF i$ = "J" OR i$ = "j" THEN restarten = 1
  IF i$ = CHR$(2) THEN CALL Help: CALL HerstelScherm: GOTO BeginRestart
'
END SUB

SUB SchrijfConfig
'
  OPEN "KALENDER.CFG" FOR RANDOM AS #1 LEN = 46
  FIELD #1, 46 AS ll$: l$ = ""
  FOR a = 1 TO 2
    FOR b = 1 TO 4
      FOR c = 1 TO 2
        l$ = l$ + CHR$(k%(a, c, b))
      NEXT c
    NEXT b
  NEXT a
  l$ = l$ + output$: LSET ll$ = l$: PUT #1, 1: CLOSE
'
END SUB

SUB StartKeuze (x, y)
'
  IF x = 1 AND y = 1 THEN CALL Info
  IF x = 1 AND y = 2 THEN CALL Installeer
  IF x = 1 AND y = 3 THEN CALL Einde
  IF x = 2 AND y = 1 THEN CALL Restart
  IF x = 2 AND y = 2 THEN CALL Bekijken
  IF x = 2 AND y = 3 THEN CALL Output1
  IF x = 2 AND y = 4 THEN CALL Zoeken
  IF x = 2 AND y = 5 THEN CALL Controleer
  IF x = 2 AND y = 6 THEN CALL Bereken1Dag
  IF x = 3 AND y = 1 THEN CALL Jaartal
  IF x = 3 AND y = 2 THEN CALL Output2
  IF x = 3 AND y = 3 THEN CALL Opstelling
  IF x = 3 AND y = 4 THEN CALL Monitor
  IF x = 4 THEN CALL Help
'
END SUB

SUB Toets (p1, p2)
'
  CALL Kaders(p1, p1 + 3, p2, p2 + 12, 1)
  COLOR k%(kl, 1, 3), k%(kl, 2, 3)
  LOCATE p1 + 1, p2 + 2: PRINT " Druk op";
  LOCATE p1 + 2, p2 + 2: PRINT "een toets";
'
END SUB

SUB VulScherm (x1, x2)
'
  COLOR k%(kl, 1, 2), k%(kl, 2, 2)
  FOR a = x1 TO x2
    LOCATE a, 1: PRINT STRING$(80, 177);
  NEXT a
'
END SUB

SUB Wacht (tijd#)
'
  tijd# = TIMER + tijd#: WHILE tijd# > TIMER
  IF INKEY$ <> "" THEN tijd# = TIMER
  WEND: tijd# = 0
'
END SUB

SUB Zoeken
'
  IF mkal = 1 THEN
    CALL Kaders(7, 9, 25, 66, 1): COLOR k%(kl, 1, 3), k%(kl, 2, 3)
    LOCATE 8, 27: PRINT "Niet mogelijk met meerdere kalenders!";
    CALL Wacht(2.5)
  END IF: IF mkal = 1 THEN EXIT SUB
'
LaatZien:
'
  CALL Kaders(3, 16, 15, 64, 1): COLOR k%(kl, 1, 3), k%(kl, 2, 3)
  FOR a = 1 TO 4
    LOCATE a + 3, 17: PRINT f$(a) + " valt op " + d$(f%(a));
    PRINT STR$(f3%(a, 1)) + " " + mnd$(f3%(a, 2))
  NEXT a
  FOR a = 5 TO 12
    LOCATE a + 3, 17: PRINT f$(a) + " valt op " + d$(f%(a)) + " " + f2$(a);
  NEXT a: CALL Toets(6, 66)
  h$ = "": WHILE h$ = "": h$ = INKEY$
  IF h$ = CHR$(2) THEN CALL Help: CALL HerstelScherm: GOTO LaatZien
  WEND
'
END SUB

