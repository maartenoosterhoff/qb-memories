' KCONFIG.BAS
' Geprogrammeerd door Maarten Oosterhoff, 1992
'
  DECLARE SUB Kleuren ()
  DECLARE SUB OutputBestand ()
'
  SCREEN 0, 0, 0, 0: TROFF: KEY OFF: COLOR 7, 0: CLS
  DIM SHARED k%(2, 2, 4), kl%(8)
'
  OPEN "KALENDER.CFG" FOR RANDOM AS #1 LEN = 8
  FIELD #1, 8 AS lees$: GET #1, 1
  IF lees$ = STRING$(8, 0) THEN
     l$ = CHR$(0) + CHR$(7) + CHR$(7) + CHR$(0) + CHR$(7)
     LSET lees$ = l$ + CHR$(0) + CHR$(0) + CHR$(7): PUT #1, 1
     l$ = CHR$(0) + CHR$(3) + CHR$(11) + CHR$(0) + CHR$(14)
     LSET lees$ = l$ + CHR$(0) + CHR$(15) + CHR$(4): PUT #1, 2
     LSET lees$ = "LPT1:": PUT #1, 3
  END IF
  FOR a = 1 TO 2: GET #1, a
    FOR b = 1 TO 4
      FOR c = 1 TO 2
        k%(a, c, b) = ASC(MID$(lees$, 2 * b - 2 + c, 1))
      NEXT c
    NEXT b: PRINT
  NEXT a
  output$ = "": FOR a = 3 TO 6: GET #1, a: FOR c = 1 TO LEN(lees$)
  code = ASC(MID$(lees$, c, 1)): IF code > 32 THEN output$ = output$ + CHR$(code)
  NEXT c, a: CLOSE
'
HoofdLus:
'
  CLS : LOCATE 1, 23: PRINT "KALENDER CONFIGURATIE PROGRAMMA";
  LOCATE 2, 1: PRINT STRING$(80, 196);
  LOCATE 3, 10: PRINT "Wegschrijfbestandsnaam:";
  LOCATE 5, 10: PRINT "Oude keuze  : "; output$;
  LOCATE 6, 10: PRINT "Nieuwe keuze: ";
  LOCATE 7, 1: PRINT STRING$(80, 196);
  LOCATE 8, 10: PRINT "Kleurensetup:";
  DEF SEG = &H40
  IF (PEEK(&H10) AND &H30) <> &H30 THEN
     LOCATE 10, 10: PRINT "Menukeuze - voorgr.  :";
     LOCATE 11, 22: PRINT "achtergr.:";
     LOCATE 13, 10: PRINT "Kaders    - voorgr.  :";
     LOCATE 14, 22: PRINT "achtergr.:";
     LOCATE 16, 10: PRINT "Tekst     - voorgr.  :";
     LOCATE 17, 22: PRINT "achtergr.:";
     LOCATE 19, 10: PRINT "Titel     - voorgr.  :";
     LOCATE 20, 22: PRINT "achtergr.:";
     FOR a = 1 TO 4
       kl%(a * 2 - 1) = k%(2, 1, a)
       kl%(a * 2) = k%(2, 2, a)
     NEXT a
     FOR a = 0 TO 15
       LOCATE a + 8, 70: PRINT USING "## = "; a;
       COLOR a: PRINT "€€€€€"; : COLOR 7, 0
     NEXT a: kleur = 1
     LOCATE 22, 10: PRINT "Gebruik voor bestand: A...Z en <BackSpace>";
     LOCATE 23, 10: PRINT "Gebruik voor kleuren: "; CHR$(24); ", "; CHR$(25); ", "; CHR$(26); " en "; CHR$(27);
  ELSE
     LOCATE 10, 10: PRINT "U heeft geen kleurenmonitor!";
     LOCATE 11, 10: PRINT "Dus ook geen kleuren om te veranderen!";
     LOCATE 13, 10: PRINT "Gebruik: A...Z, <BackSpace>, <Enter> en <Esc>";
     LOCATE 14, 1: PRINT STRING$(80, 196); : kleur = 0
  END IF: DEF SEG : out$ = "": k = 1: eind = 0
  LOCATE 24, 1: PRINT STRING$(80, 196);
'
Configuratie:
'
  LOCATE 6, 24: PRINT out$; : COLOR 23, 0: PRINT "_ ";
  IF kleur = 1 THEN
     COLOR 7, 0
     FOR a = 1 TO 8 STEP 2
       LOCATE a * 1.4 + 9, 33: PRINT USING "##"; kl%(a);
       LOCATE a * 1.4 + 10, 33: PRINT USING "##"; kl%(a + 1);
     NEXT a
     COLOR 23, 0: x = k + 9
     IF k > 2 THEN x = x + 1
     IF k > 4 THEN x = x + 1
     IF k > 6 THEN x = x + 1
     LOCATE x, 33, 1
  END IF: COLOR 7, 0
  i$ = INKEY$: WHILE i$ = "": i$ = INKEY$: WEND: ok = 0
  LOCATE 25, 1, 0: PRINT STRING$(80, 32);
  IF i$ = CHR$(8) THEN
     ok = 1
     IF LEN(out$) = 0 THEN
        SOUND 1000, .1
     ELSE
        out$ = LEFT$(out$, LEN(out$) - 1)
     END IF
  END IF
  IF LEN(out$) < 30 AND INSTR("ABCDEFGHIJKLMNOPQRSTUVWXYZ1234567890-.=:\", UCASE$(i$)) > 0 THEN out$ = out$ + UCASE$(i$): ok = 1
  IF kleur = 1 THEN
     IF i$ = CHR$(0) + CHR$(80) THEN ok = 1: k = k + 1: IF k = 9 THEN k = 1
     IF i$ = CHR$(0) + CHR$(72) THEN ok = 1: k = k - 1: IF k = 0 THEN k = 8
     IF i$ = CHR$(0) + CHR$(75) THEN ok = 1: kl%(k) = kl%(k) - 1: IF kl%(k) = -1 THEN kl%(k) = 15
     IF i$ = CHR$(0) + CHR$(77) THEN ok = 1: kl%(k) = kl%(k) + 1: IF kl%(k) = 16 THEN kl%(k) = 0
  END IF
  x = INSTR(out$, "\"): WHILE x <> 0: y = x: x = INSTR(x + 1, out$, "\"): WEND
  IF i$ = CHR$(13) THEN x$ = RIGHT$(out$, LEN(out$) - y)
  IF i$ = CHR$(13) AND (x$ = "KALENDER.EXE" OR x$ = "KALENDER.DAT" OR x$ = "SCRN:" OR x$ = "CON" OR x$ = "PRN" OR x$ = "KALENDER.DOC" OR x$ = "KALENDER.HLP" OR x$ = "KALENDER.CFG" OR x$ = "KINSTALL.BAT" OR x$ = "KCONFIG.EXE") THEN SOUND 1000, .1:   _
LOCATE 25, 10: PRINT "Ongeldige naam!"; : GOTO Configuratie
  IF i$ = CHR$(27) THEN ok = 1: GOSUB Einde: IF eind = 1 THEN CLS : END
  IF i$ = CHR$(13) THEN ok = 1: GOSUB Bewaar: GOSUB Einde: IF eind = 1 THEN CLS : END ELSE GOTO HoofdLus
  IF ok = 0 THEN SOUND 1000, .1
  GOTO Configuratie
'
Einde:
'
  LOCATE 25, 10, 1: PRINT "Kalender Configuratie beâindigen (J/N) ? "; : i$ = ""
  WHILE UCASE$(i$) <> "N" AND UCASE$(i$) <> "J": i$ = INKEY$: WEND
  IF UCASE$(i$) = "J" THEN eind = 1
  LOCATE 25, 1, 0: PRINT STRING$(80, 32); : RETURN
'
Bewaar:
'
  LOCATE 25, 10, 1: PRINT "Ingevoerde gegevens bewaren (J/N) ? "; : i$ = ""
  WHILE UCASE$(i$) <> "J" AND UCASE$(i$) <> "N": i$ = INKEY$: WEND
  IF UCASE$(i$) = "J" THEN
     FOR a = 1 TO 4
       k%(2, 1, a) = kl%(a * 2 - 1)
       k%(2, 2, a) = kl%(a * 2)
     NEXT a: KILL "KALENDER.CFG"
     OPEN "KALENDER.CFG" FOR RANDOM AS #1 LEN = 8
     FIELD #1, 8 AS lees$: GET #1, 1
     FOR a = 1 TO 2: l$ = STRING$(8, 0)
       FOR b = 1 TO 4
         FOR c = 1 TO 2
           MID$(l$, 2 * b - 2 + c, 1) = CHR$(k%(a, c, b))
         NEXT c
       NEXT b: LSET lees$ = l$: PUT 1, a
     NEXT a
     IF out$ > "" THEN output$ = out$ ELSE out$ = output$
     LSET lees$ = out$: PUT #1, 3
     IF LEN(out$) > 8 THEN LSET lees$ = RIGHT$(out$, LEN(out$) - 8): PUT #1, 4
     IF LEN(out$) > 16 THEN LSET lees$ = RIGHT$(out$, LEN(out$) - 16): PUT #1, 5
     IF LEN(out$) > 24 THEN LSET lees$ = RIGHT$(out$, LEN(out$) - 24): PUT #1, 6
     CLOSE #1
  END IF: RETURN

